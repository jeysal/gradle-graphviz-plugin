plugins {
    id 'groovy'
    id 'java-gradle-plugin'
}

group = 'com.github.jeysal'
version = ['git', 'describe', '--tags'].execute([], projectDir).inputStream.text.trim()

sourceCompatibility = 1.8
targetCompatibility = 1.8

gradlePlugin {
    plugins {
        graphviz {
            id = 'com.github.jeysal.graphviz'
            implementationClass = 'com.github.jeysal.gradle.plugin.graphviz.GraphvizPlugin'
        }
    }
}

// Dependencies

repositories {
    mavenCentral()
    maven { url 'https://plugins.gradle.org/m2/' }
}

dependencies {
    // Node
    implementation 'com.github.node-gradle:gradle-node-plugin:2.2.4'
    implementation 'org.asciidoctor:asciidoctor-gradle-jvm:3.3.2'

    // Test
    testImplementation('org.spockframework:spock-core:2.2-groovy-3.0') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
}

// Test

test {
    useJUnitPlatform()
}

// Artifacts

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    group = classes.group
    from sourceSets.main.allSource
}
task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    group = groovydoc.group
    from groovydoc.destinationDir
}

artifacts {
    archives sourceJar
    archives groovydocJar
}
