plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven'
}

group = 'com.github.jeysal'
version = ['git', 'describe', '--tags'].execute([], projectDir).inputStream.text.trim()

sourceCompatibility = 1.8
targetCompatibility = 1.8

gradlePlugin {
    plugins {
        graphviz {
            id = 'com.github.jeysal.graphviz'
            implementationClass = 'com.github.jeysal.gradle.plugin.graphviz.GraphvizPlugin'
        }
    }
}

// Dependencies

repositories {
    jcenter()
}

dependencies {
    // Node
    compile 'com.moowork.gradle:gradle-node-plugin:0.13'

    // Test
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
}

// Artifacts

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    group = classes.group
    from sourceSets.main.allSource
}
task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    group = groovydoc.group
    from groovydoc.destinationDir
}

artifacts {
    archives sourceJar
    archives groovydocJar
}
